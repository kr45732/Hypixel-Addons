/*
 * Hypixel Addons - A customizable quality of life mod for Hypixel
 * Copyright (c) 2021 kr45732
 *
 * This program is free software: you can redistribute it and/or modify
 * it under the terms of the GNU Affero General Public License as
 * published by the Free Software Foundation, either version 3 of the
 * License, or (at your option) any later version.
 *
 * This program is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU Affero General Public License for more details.
 *
 * You should have received a copy of the GNU Affero General Public License
 * along with this program.  If not, see <https://www.gnu.org/licenses/>.
 */

pluginManagement {
    repositories {
        mavenCentral()
        gradlePluginPortal()

        // Add the Forge Repository (ForgeGradle fetches most of its stuff from here)
        maven {
            name = "Forge"
            url = "https://maven.minecraftforge.net"
        }

        // Add the Jitpack Repository (We fetch ForgeGradle from this)
        maven {
            name = "Jitpack"
            url = "https://jitpack.io/"
        }
    }
    resolutionStrategy {
        eachPlugin {
            // If the "net.minecraftforge.gradle.forge" plugin is requested we redirect it to asbyth's ForgeGradle fork
            switch (requested.id.id) {
                case 'net.minecraftforge.gradle.forge':
                    useModule "com.github.asbyth:ForgeGradle:${requested.version}"
                    break
                case 'org.spongepowered.mixin':
                    useModule "com.github.xcfrg:MixinGradle:${requested.version}"
                    break
            }
        }
    }
}

rootProject.name = "HypixelAddons"